import org.apache.tools.ant.taskdefs.condition.Os

buildscript {
    ext {
        springBootVersion = spring_boot_version
    }

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'org.springframework:springloaded:1.2.3.RELEASE'
    }
}

apply plugin: 'java'
apply plugin: 'eclipse-wtp'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'war'

war {
    baseName = 'FormReleaf'
    version = getBuildVersion()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'http://repo.spring.io/milestone' }
    maven { url 'http://repo.spring.io/snapshot' }
    maven { url 'https://repository.jboss.org/nexus/content/repositories/releases' }
    maven { url 'https://oss.sonatype.org/content/repositories/releases' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'http://repo.maven.apache.org/maven2' }
}

configurations {
    providedRuntime
}

bootRepackage {
    mainClass = 'com.vantage.sportsregistration.Application'
}

springBoot {
    mainClass = 'com.vantage.sportsregistration.Application'
}

bootRun {
//    addResources = false
}

if (project.hasProperty('prod')) {
    apply from: 'profile_prod.gradle'
} else if (project.hasProperty('stg')) {
    apply from: 'profile_stg.gradle'
} else {
    apply from: 'profile_dev.gradle'
}

dependencies {
    compile project(":formreleaf-common")
    compile project(":formreleaf-domain")
    compile project(":formreleaf-dao")
    compile project(":formreleaf-report")

    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-thymeleaf")
    //compile "org.springframework.boot:spring-boot-starter-data-jpa"
    compile "org.springframework.boot:spring-boot-starter-security"
    compile 'org.thymeleaf.extras:thymeleaf-extras-springsecurity3:2.1.1.RELEASE'

    compile 'com.fasterxml.jackson.core:jackson-databind:2.2.+'
    compile 'com.fasterxml.jackson.core:jackson-core:2.2.+'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.2.+'

    compile 'org.postgresql:postgresql:9.4-1201-jdbc41'

    compile 'org.webjars:bootstrap:3.3.1'        // ref: www.webjars.org
    compile 'org.webjars.bower:angular:1.3.15'
    compile 'org.webjars:jReject:1.1.0'

    compile group: 'org.apache.geronimo.javamail', name: 'geronimo-javamail_1.4_mail', version: '1.8.4'
    compile(group: 'org.springframework', name: 'spring-context-support') {
        exclude(module: 'quartz')
    }

    compile 'commons-codec:commons-codec:1.10'
    compile 'org.apache.poi:poi:3.12'
    compile 'org.json:json:20141113'
    compile 'commons-io:commons-io:2.4'
    compile 'commons-validator:commons-validator:1.4.1'
    compile 'commons-fileupload:commons-fileupload:1.3.1'
    compile 'org.bouncycastle:bcprov-jdk15on:1.52'

    //pdf
    compile 'com.itextpdf:itextpdf:5.5.6'
    compile 'com.itextpdf.tool:xmlworker:5.5.6'

    //html cleanup
    compile 'net.sf.jtidy:jtidy:r938'

    //super csv
    compile 'net.sf.supercsv:super-csv:2.3.1'
    compile 'net.sf.supercsv:super-csv-dozer:2.3.1'
    
    //hibernate ehcache
    compile 'net.sf.ehcache:ehcache:2.9.0'
    compile 'org.springframework:spring-context-support'

    compile 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect:1.2.5'
    compile 'javax.mail:mail:1.4'
    compile 'org.codehaus.janino:janino:2.7.8'

    compile 'javax.servlet:javax.servlet-api:3.1.0'
    
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-tomcat', version: spring_boot_version
    testCompile("org.springframework.boot:spring-boot-starter-test")
}

eclipse {
    classpath {
        containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
        containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
    }
}

idea {
    module {
        inheritOutputDirs = false
        outputDir = file("$buildDir/classes/main/")
    }
}

task stage(dependsOn: 'bootRepackage') {
}

def getArtifactBuildNumber() {
    File propsFile

    try {
        if (Os.isFamily(Os.FAMILY_WINDOWS)) {
            propsFile = new File('D:\\formreleaf.config')
        } 
        else {
            propsFile = new File(System.getenv('HOME') + '/formreleaf.config')
        }

        Properties props = new Properties()
        props.load(propsFile.newDataInputStream())
        Integer nextBuildNum = (((props.getProperty('artifactBuildNumber')) as BigDecimal) + 1)
        props.setProperty('artifactBuildNumber', nextBuildNum.toString())
        props.setProperty('buildDate', new Date().toString())
        props.store(propsFile.newWriter(), null)
        props.load(propsFile.newDataInputStream())

        return props.getProperty('artifactBuildNumber');
    }
    catch (ignored){
        return 0;
    }

}

def getBuildVersion() {

    return "$artifactMajorVersion-${getArtifactBuildNumber()}-SNAPSHOT"
}